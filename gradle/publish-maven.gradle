apply plugin: 'maven-publish'

publishing {

  repositories {
    maven {
      def releasesRepoUrl = "${nexusReleasesUrl}"
      def snapshotsRepoUrl = "${nexusSnapshotsUrl}"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = "${nexusUsername}"
        password = "${nexusPassword}"
      }
      logger.info("publishing '{}' with version '{}' to '{}'", project.name, version, url)
    }
  }
}

//now uploading will have to be configured on a per subproject basis
subprojects {

  publishing {

    publications {
      "$project.name"(MavenPublication) {

        afterEvaluate {
          from components.java
          artifactId = project.name
          pom {
            name = project.name
            description = project.description
          }
        }

        pom {

          url = "https://github.com/openEHR/archie"
          organization {
            name = "Nedap Healthcare"
            url = "http://www.nedap-healthcare.com"
          }
          licenses {
            license {
              name = "Apache License, Version 2.0"
              url = "http://www.apache.org/licenses/LICENSE-2.0"
              distribution = "repo"
            }
          }
          scm {
            url = "https://github.com/openEHR/archie"
            connection = "scm:git:git://github.com/openEHR/archie"
            developerConnection = "scm:git:git://github.com/openEHR/archie"
          }
          developers {
            developer {
              id = "pieterbos"
              name = "Pieter Bos"
              email = "pieter.bos@nedap.com"
            }
            developer {
              id = "tanja.dejong"
              name = "Tanja de Jong"
              email = "tanja.dejong@nedap.com"
            }
          }
          issueManagement {
            system = "Github"
            url = "https://github.com/openEHR/archie/issues"
          }
        }
      }
    }
  }

}
